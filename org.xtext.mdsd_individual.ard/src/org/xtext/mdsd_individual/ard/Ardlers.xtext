grammar org.xtext.mdsd_individual.ard.Ardlers with org.eclipse.xtext.common.Terminals

generate ardlers "http://www.xtext.org/mdsd_individual/ard/Ardlers"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Program: (board=Library? sensorsImports+=SensorImport*  program+= (Rule | Node)*) | definitions+= BoardDefinition+;

SensorImport:
	'sensor' name= ID
;

Library:
	'board' boardtype = [BoardDefinition]
;

BoardDefinition:
	'define' 'board' name=ID 
	('din:' di+= INT (',' di+= INT)*) & 
	('dout:' do+= INT (',' do+= INT)*) & 
	('ain:' ain+= INT (',' ain+=INT)*) & 
	('aout:' aout+=INT (',' aout+=INT)*)
;

Rule:
    type=('when' | 'once') condition=Or '{' body=RuleBody '}'
;

Or:
	And ({Or.left=current} operator=Orop right=And)*
;

And returns Expression:
	Comparison ({And.left=current} operator=Andop right=Comparison)*
;

Comparison returns Expression:
	Exp ({Comparison.left=current} operator=EquallityOp right=Exp)*
;

Exp returns Expression:
	Factor ({Exp.left=current} operator=PlusMinusOp  right=Factor)*
;

Factor returns Expression:
    Value ({Factor.left=current} operator=MultiplicationDivideOp right=Value)*
;

PlusMinusOp:
	('+' | '-')
;

MultiplicationDivideOp:
	('*' | '/') 
;

EquallityOp:
	('<' | '>' | '>=' | '<=' | '==' | '!=')
;
Andop:
	"&&"
;
Orop:
	"||"
;


Value:
     NumberLiteral | Attribute | Delta
;

Attribute:
    name=[Node]  component=[Component]
;

Delta:
    attr=Attribute 'delta'
;

NumberLiteral:
	float=DECIMAL | int=INT
;

terminal DECIMAL: INT '.' INT;

NUMBER returns ecore::EFloat: INT ('.' INT)?;

State:
    value=('on' | 'off')
;

RuleBody:
    assignment+= Assignment+
;

Assignment:
    attribute=Attribute "=" value=(Exp | State)
;

Node:
    name=ID '{' components+=Component+ '}'
;

Component:
    name=ID (('using' sensor=[SensorImport] properties= PinlessComponentBody) | properties=ComponentBody)
;

PinlessComponentBody returns ComponentBody:
    io=IO rate=Rate? map=Map?
;

ComponentBody:
    io=IO type=TYPE pinned='pin' pin=INT rate=Rate? map=Map?
;

enum IO:
    INPUT='input' | OUTPUT='output'
;

enum TYPE:
    ANALOG='analog' | DIGITAL='digital'
;

Map:
    'map' in=Range '=>' out=Range
;

Range:
    low=NUMBER ':' high=NUMBER
;

Rate:
    'rate' value=INT
;

Smoothing:
    'smoothing' value=NUMBER
;